{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "6adcb37f-255a-47d5-b602-9cddcd6c5dd3",
   "metadata": {},
   "source": [
    "# MATH2504 Project 2 2022 Semester 2 Submission\n",
    "\n",
    "[Assignment Instructions](https://courses.smp.uq.edu.au/MATH2504/2022/assessment_html/project2.html)\n",
    "\n",
    "Student names: Limao Chang, Tiarne Graves"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "1e0ed7ee-be9f-4654-b3b5-ab4d621e8162",
   "metadata": {},
   "outputs": [],
   "source": [
    "include(\"simulation_script.jl\")\n",
    "include(\"test/scenarios.jl\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "637f529e",
   "metadata": {},
   "source": [
    "## Project Structure\n",
    "\n",
    "In the root folder is `simulation_script.jl`, which imports and loads the `GeneralizedJacksonSim` module, as well as some other packages that are useful when working with `GeneralizedJacksonSim`.\n",
    "\n",
    "The `src/` folder contains the source code for `GeneralizedJacksonSim`.\n",
    "\n",
    "The `test/` folder contains tests which can be run in `test/runtests.jl`, as well as the four provided test scenarios, which can be found in `test/scenarios.jl`."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "17d94db0-2946-4f54-a206-890a9b63b349",
   "metadata": {},
   "source": [
    "## Perspective Seminar"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8e74fd18-8265-47a3-8c9f-cb5046a8cc52",
   "metadata": {},
   "source": [
    "## Task 1"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a833d707",
   "metadata": {},
   "source": [
    "**Features of amusement park that are not captured by model**:\n",
    "- Amusement park attractions usually serve multiple people simultaneously, whereas the model assumes jobs can only be serviced one at a time\n",
    "- People may cut in line and skip queues, so in reality it is not really a queue in the first-in, first-out (FIFO) sense\n",
    "\n",
    "**Model assumptions that are unrealistic for the application of a theme park**:\n",
    "- Assuming that queues (and by extension the theme park) have infinite capacity\n",
    "- Assuming that people will unconditionally stay in a queue until they get to the front of the line - they may leave because the queue is too long, they want to go to another attraction, etc.\n",
    "- Assuming that the time taken to move between attractions or leave the theme park is instant, as travelling between attractions takes time\n",
    "- Service duration is independent from all other services - if people are at the theme park together as a party (e.g. a family travelling around the theme park), their service durations will not be independent from each other\n",
    "- Arrival of jobs to nodes is exponentially distributed - the arrival rate will likely be higher during the day (more traffic) than in the evening, for example.\n",
    "\n",
    "**If you were actually using this model to assess congestion at the park, would it be useful or not?**"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b64166d8-4798-41a1-9d32-bfbd78ebfe51",
   "metadata": {},
   "source": [
    "## Task 2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dc0af862-489a-495f-b64e-3a1f6532fffa",
   "metadata": {},
   "source": [
    "The total steady state mean queue lengths are plotted against $\\rho$* below for the four scenarios."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "c21fbb9d-8dfa-4b1e-a208-f2156c66576c",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "    plot_total_ss_mean_queue_length(net::NetworkParameters)\n",
    "\n",
    "Plots total steady state mean queue length as a function of ρ* for the given scenario.\n",
    "\"\"\"\n",
    "function plot_total_ss_mean_queue_length(net::NetworkParameters, scenario_number::Int64)\n",
    "    ρ_star_values = 0.1:0.01:0.9\n",
    "    total_ss_mean_queue_lengths = zeros(length(ρ_star_values))\n",
    "\n",
    "    for (index, ρ_star) in enumerate(ρ_star_values)\n",
    "        # adjust network parameters\n",
    "        adjusted_scenario = set_scenario(net, ρ_star)\n",
    "        ρ = compute_ρ(adjusted_scenario)\n",
    "\n",
    "        total_ss_mean_queue_lengths[index] = sum(ρ ./ (1 .- ρ))\n",
    "    end\n",
    "\n",
    "    return plot(ρ_star_values,\n",
    "                total_ss_mean_queue_lengths,\n",
    "                xlabel=\"ρ\",\n",
    "                ylabel=\"Total Steady State Mean Queue Length\",\n",
    "                title=\"Scenario $scenario_number Theoretical\")\n",
    "end\n",
    "\n",
    "p1 = plot_total_ss_mean_queue_length(scenario1, 1)\n",
    "p2 = plot_total_ss_mean_queue_length(scenario2, 2)\n",
    "p3 = plot_total_ss_mean_queue_length(scenario3, 3)\n",
    "p4 = plot_total_ss_mean_queue_length(scenario4, 4)\n",
    "        \n",
    "plot(p1, p2, p3, p4, layout=(2, 2), legend=false, size=(800, 800))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1d1ec41f-cb94-46fa-9703-355d3491d9dc",
   "metadata": {},
   "source": [
    "## Task 3"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "82bc8081-955a-4298-a65b-6861ffb47dbe",
   "metadata": {},
   "source": [
    "**Simulation engine**: `src/GeneralizedJacksonSim.jl` contains the `GeneralizedJacksonSim` module, as well as the simulation engine used (`sim_net()` below).\n",
    "\n",
    "```julia\n",
    "# src/GeneralizedJacksonSim.jl\n",
    "\"\"\"\n",
    "A discrete event simulation engine for Open Generalized Jackson Networks.\n",
    "\"\"\"\n",
    "module GeneralizedJacksonSim\n",
    "\n",
    "import Base: isless\n",
    "using Accessors, DataStructures, Distributions, StatsBase, Parameters, LinearAlgebra,\n",
    "    Random, Plots\n",
    "\n",
    "include(\"network_parameters.jl\")\n",
    "include(\"state.jl\")\n",
    "include(\"event.jl\")\n",
    "\n",
    "export NetworkParameters, compute_ρ, maximal_alpha_scaling, set_scenario, sim_net\n",
    "\n",
    "\"\"\"\n",
    "Runs a discrete event simulation of an Open Generalized Jackson Network `net`.\n",
    "\n",
    "The simulation runs from time `0` to `max_time`.\n",
    "\n",
    "Statistics about the total mean queue lengths are recorded from `warm_up_time` onwards\n",
    "and the estimated value is returned.\n",
    "\n",
    "This simulation does NOT keep individual customers' state, it only keeps the state which is\n",
    "the number of items in each of the nodes.\n",
    "\"\"\"\n",
    "function sim_net(net::NetworkParameters;\n",
    "                 max_time=10^6, warm_up_time=10^4, seed::Int64=42)::Float64\n",
    "    Random.seed!(seed)\n",
    "\n",
    "    # create priority queue and add standard events\n",
    "    priority_queue = BinaryMinHeap{TimedEvent}()\n",
    "    for q in 1:net.L\n",
    "        push!(priority_queue, TimedEvent(ExternalArrivalEvent(q), 0.0))\n",
    "    end\n",
    "    push!(priority_queue, TimedEvent(EndSimEvent(), max_time))\n",
    "\n",
    "    # initialise state and time\n",
    "    state = QueueNetworkState(zeros(Int64, net.L), net.L, net)\n",
    "    time = 0.0\n",
    "\n",
    "    # set up queues integral for computing total mean queue length\n",
    "    queues_integral = zeros(net.L)\n",
    "    last_time = 0.0\n",
    "\n",
    "    \"\"\"\n",
    "    Records the queue integral of the given state at the given point in time.\n",
    "    \"\"\"\n",
    "    function record_integral(time::Float64, state::State)\n",
    "        (time >= warm_up_time) && (queues_integral += state.queues * (time - last_time))\n",
    "        last_time = time\n",
    "    end\n",
    "\n",
    "    record_integral(time, state)\n",
    "\n",
    "    # simulation loop\n",
    "    while true\n",
    "        # process the next upcoming event\n",
    "        timed_event = pop!(priority_queue)\n",
    "        time = timed_event.time\n",
    "        new_timed_events = process_event(time, state, timed_event.event)\n",
    "\n",
    "        isa(timed_event.event, EndSimEvent) && break\n",
    "\n",
    "        # add new spawned events to queue\n",
    "        for nte in new_timed_events\n",
    "            push!(priority_queue, nte)\n",
    "        end\n",
    "\n",
    "        # record mean queue length\n",
    "        record_integral(time, state)\n",
    "    end\n",
    "\n",
    "    return sum(queues_integral / max_time)\n",
    "end\n",
    "\n",
    "end  # end of module\n",
    "```\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f9752028-0e8a-40c6-8393-3d53d29afc29",
   "metadata": {},
   "source": [
    "The other functionalities of the simulation engine (`NetworkParameters`, `State`s, `Event`s, and the functions related to each of them) have been separated into their own files, to keep the code nice and structured. Below are some snippets from each file:\n",
    "\n",
    "```julia\n",
    "# src/network_parameters.jl\n",
    "\"\"\"\n",
    "    NetworkParameters\n",
    "\n",
    "# Fields\n",
    "- `L::Int`: the dimension of the network (number of nodes)\n",
    "- `α_vector::Vector{Float64}`: the external arrival rates α_i >= 0\n",
    "- `μ_vector::Vector{Float64}`: the service rates μ_i > 0\n",
    "- `P::Matrix{Float64}`: the L×L routing matrix P\n",
    "- `c_s::Float64`: squared coefficient of variation of the service processes, defaults to 1.0\n",
    "\"\"\"\n",
    "@with_kw struct NetworkParameters\n",
    "    L::Int\n",
    "    α_vector::Vector{Float64}\n",
    "    μ_vector::Vector{Float64}\n",
    "    P::Matrix{Float64}\n",
    "    c_s::Float64 = 1.0\n",
    "end\n",
    "\n",
    "# src/state.jl\n",
    "\"\"\"\n",
    "    QueueNetworkState\n",
    "\n",
    "# Fields\n",
    "- `queues::Vector{Int}`: vector of number of customers in each queue\n",
    "- `num_queues::Int`: number of queues, equivalent to `length(queues)`\n",
    "- `net::NetworkParameters`: the parameters of the network\n",
    "\"\"\"\n",
    "mutable struct QueueNetworkState <: State\n",
    "    queues::Vector{Int}\n",
    "    num_queues::Int\n",
    "    net::NetworkParameters\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    next_arrival_time(s::State, q::Int)\n",
    "\n",
    "Generates the next arrival time for the `q`th server. The duration of time between\n",
    "external arrivals is exponentially distributed with mean 1 / s.net.α_vector[q].\n",
    "\"\"\"\n",
    "next_arrival_time(s::State, q::Int) = rand(Exponential(1/s.net.α_vector[q]))\n",
    "\n",
    "\"\"\"\n",
    "    next_service_time(s::State, q::Int)\n",
    "\n",
    "Generates the next service time for the `q`th server. The service duration is gamma\n",
    "distributed.\n",
    "\"\"\"\n",
    "next_service_time(s::State, q::Int) = rand(rate_scv_gamma(s.net.μ_vector[q], s.net.c_s))\n",
    "\n",
    "# src/event.jl\n",
    "\"\"\"\n",
    "    process_event(time::Float64, state::State, event::ExternalArrivalEvent)\n",
    "\n",
    "Process an arrival event from outside the system, and spawns a list of events that occur as\n",
    "a consequence of this arrival.\n",
    "\n",
    "On arrival, if the server is free (no jobs in the buffer/queue), the job starts to receive\n",
    "service. If the server is busy, the job queues for service and waits for its turn.\n",
    "\n",
    "The time between external arrival events for a given server is exponentially distributed,\n",
    "and the service duration is gamma distributed.\n",
    "\"\"\"\n",
    "function process_event(time::Float64, state::State, event::ExternalArrivalEvent)\n",
    "    q = event.q\n",
    "    state.queues[q] += 1  # add to queue\n",
    "    new_timed_events = TimedEvent[]\n",
    "\n",
    "    # prepare next external arrival for this particular server\n",
    "    push!(new_timed_events,\n",
    "        TimedEvent(ExternalArrivalEvent(q), time + next_arrival_time(state, q)))\n",
    "\n",
    "    # start serving this job if it is the only one in the queue\n",
    "    if state.queues[q] == 1\n",
    "        push!(new_timed_events,\n",
    "            TimedEvent(EndOfServiceEvent(q), time + next_service_time(state, q)))\n",
    "    end\n",
    "    return new_timed_events\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    process_event(time::Float64, state::State, event::EndOfServiceEvent)\n",
    "\n",
    "Process an end-of-service event, and spawns a list of events that occur as a consequence of\n",
    "this end of service.\n",
    "\n",
    "When a job completes service at a buffer, it either leaves the system, or moves to another\n",
    "buffer (both happen immediately). After completing service in server i, a job moves to\n",
    "server j with probability P[i, j], where P is the routing matrix.\n",
    "\"\"\"\n",
    "function process_event(time::Float64, state::State, event::EndOfServiceEvent)\n",
    "    q = event.q\n",
    "    state.queues[q] -= 1  # remove from queue\n",
    "    @assert state.queues[q] >= 0\n",
    "    new_timed_events = TimedEvent[]\n",
    "\n",
    "    # if there is another customer in the queue, start serving them\n",
    "    if state.queues[q] > 0\n",
    "        service_time = next_service_time(state, q)\n",
    "        push!(new_timed_events, TimedEvent(EndOfServiceEvent(q), time + service_time))\n",
    "    end\n",
    "\n",
    "    # simulate the next location for this job; indices 1:L are the probabilities of moving\n",
    "    # to another server in the system, and the last index is the probability of exiting\n",
    "    # the system\n",
    "    L = state.net.L\n",
    "    next_loc_weights = state.net.P[q, :]\n",
    "    push!(next_loc_weights, 1 - sum(next_loc_weights))\n",
    "    @assert sum(next_loc_weights) == 1\n",
    "    next_loc = sample(1:L+1, Weights(next_loc_weights))\n",
    "\n",
    "    if next_loc <= L\n",
    "        # job is staying in the system\n",
    "        state.queues[next_loc] += 1\n",
    "\n",
    "        # start serving job if it is the only one in the queue\n",
    "        if state.queues[next_loc] == 1\n",
    "            service_time = next_service_time(state, next_loc)\n",
    "            push!(new_timed_events,\n",
    "                TimedEvent(EndOfServiceEvent(next_loc), time + service_time))\n",
    "        end\n",
    "    end\n",
    "    return new_timed_events\n",
    "end\n",
    "```"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2aeb7ce3-8b99-4298-94f4-8cae0c3c220e",
   "metadata": {},
   "source": [
    "**Test 1**: the code for test 1 can be found in `test/task3_test1.jl`. We decided to advantage of multi-threading for this test, since each simulation can be run independently, and their results can be stored without affecting any other simulations (see the source code for more details). To make use of multi-threading, you have to run Julia with the `-t` or `--threads` option, and specify the number of threads you want to utilise, e.g. `julia -t 8` to run Julia with 8 threads. The test can then by executed using:\n",
    "```julia\n",
    "task3_test1(\n",
    "    [scenario1, scenario2, scenario3, scenario4],  # vector of all scenarios to test\n",
    "    verbose=false,  # whether to print out progress messages as the simulations are run\n",
    "    multithreaded=true  # whether to utilise multi-threading (julia needs to be run with\n",
    "                        # `julia -t n` where n is the number of threads)\n",
    ")\n",
    "```\n",
    "\n",
    "An example output is given below, for `max_time` ranging from 10^3 to 10^5 (beyond 10^5 is unreasonable for scenario 4 - it took around half an hour to run scenario 4 with `max_time = 10^6`). In every scenario, the absolute relative errors seem to decrease as $\\rho^*$ gets larger, and also as `max_time` gets larger.\n",
    "\n",
    "<center>\n",
    "    <img src=\"img/task3test1.png\" width=\"640\" />\n",
    "</center>"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9cd5a534-5fe4-400a-8d64-e62c7fcf54a3",
   "metadata": {},
   "source": [
    "## Task 4"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1e497465-6cb3-411e-9e02-f36eb8b96ee0",
   "metadata": {},
   "source": [
    "## Task 5"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.3",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
